plugins {
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

//apply plugin: 'org.spongepowered.mixin'
if (project.use_mixins.toBoolean()) {
	apply plugin: 'org.spongepowered.mixin'
}

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)


if (project.use_intellij_idea.toBoolean()) {
	apply {
        plugin 'java'
        plugin 'idea'
    }


    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

if (project.use_eclipse.toBoolean()) {
	apply {
        plugin 'java'
        plugin 'eclipse'
    }


    eclipse {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}


minecraft {

    mappings channel: 'stable', version: '39-1.12'

	if (project.has_access_transformer.toBoolean()) {
		accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	}

    runs {
        client {
            workingDirectory project.file('run')
			if (project.use_coremod.toBoolean()) {
				jvmArg '-Dfml.coreMods.load=' + coremod_plugin_class_name
			}
			if (project.use_mixins.toBoolean()) {
				jvmArg '-Dmixin.hotSwap=true'
				jvmArg '-Dmixin.checks.interfaces=true'
				jvmArg '-Dmixin.debug=true'
			}
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            environment 'MC_VERSION', '1.12.2'
        }

        server {
            workingDirectory project.file('run')
            if (project.use_coremod.toBoolean()) {
				jvmArg '-Dfml.coreMods.load=' + coremod_plugin_class_name
			}
			if (project.use_mixins.toBoolean()) {
				jvmArg '-Dmixin.hotSwap=true'
				jvmArg '-Dmixin.checks.interfaces=true'
			}
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            environment 'MC_VERSION', '1.12.2'
        }
    }
}


repositories {
	maven { 
		url = 'https://maven.cleanroommc.com' 
	}
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url = "https://chickenbones.net/maven"
    }
    maven {
        url "https://maven.blamejared.com/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
        allowInsecureProtocol = true
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        // location for sources
        dirs 'libs'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    if (project.use_mixins.toBoolean()) {
        compileOnly 'zone.rong:mixinbooter:7.0'
        runtimeOnly 'zone.rong:mixinbooter:7.0'

        annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    }
    //implementation fg.deobf("mezz.jei:jei_1.12.2:4.16.1.302")
    //implementation fg.deobf("curse.maven:baubles-227083:2518667")
    implementation fg.deobf("codechicken:CodeChickenLib:1.12.2-3.2.3.358:universal")
    implementation fg.deobf("curse.maven:minefantasy-reforged-431996:5553214")
    //implementation fg.deobf("curse.maven:animaniabase-261199:3213136")
    //implementation fg.deobf("curse.maven:animaniafarm-407632:3213139")

    compileOnly fg.deobf("curse.maven:simple-corn-246761:2854990")
    if (project.debug_simple_corn.toBoolean()) {
        runtimeOnly fg.deobf("curse.maven:simple-corn-246761:2854990")
    }

    compileOnly fg.deobf("curse.maven:rustic-256141:3107974")
    if (project.debug_rustic.toBoolean()) {
        runtimeOnly fg.deobf("curse.maven:rustic-256141:3107974")
    }

    compileOnly ("com.animania:animania-1.12.2-base:2.0.3.28")
    compileOnly ("com.animania:animania-1.12.2-catsdogs:1.0.0.27")
    compileOnly ("com.animania:animania-1.12.2-extra:1.0.2.28")
    compileOnly ("com.animania:animania-1.12.2-farm:1.0.2.28")
    if (project.debug_animania_base.toBoolean()) {
        runtimeOnly ("com.animania:animania-1.12.2-base:2.0.3.28")
    }
    if (project.debug_animania_catsdogs.toBoolean()) {
        runtimeOnly ("com.animania:animania-1.12.2-catsdogs:1.0.0.27")
    }
    if (project.debug_animania_extra.toBoolean()) {
        runtimeOnly ("com.animania:animania-1.12.2-extra:1.0.2.28")
    }
    if (project.debug_animania_farm.toBoolean()) {
        runtimeOnly ("com.animania:animania-1.12.2-farm:1.0.2.28")
    }

    compileOnly fg.deobf("curse.maven:betterwitheverything-896908:5202745")
    if (project.debug_betterwitheverything.toBoolean()) {
        runtimeOnly fg.deobf("curse.maven:betterwitheverything-896908:5202745")
    }

    compileOnly fg.deobf("curse.maven:spartan-weaponry-278141:5564836")
    if (project.debug_spartanweaponry.toBoolean()) {
        //runtimeOnly fg.deobf("curse.maven:spartan-weaponry-278141:5564836")
    }

    compileOnly fg.deobf("curse.maven:charcoal-pit-264111:2615892")
    if (project.debug_charcoal_pit.toBoolean()) {
        runtimeOnly fg.deobf("curse.maven:charcoal-pit-264111:2615892")
    }

    compileOnly fg.deobf("curse.maven:biomes-o-plenty-220318:3558882")
    compileOnly fg.deobf("curse.maven:rusticated-fruit-547435:3533340")
    if (project.debug_rusticatedfruit.toBoolean()) {
        runtimeOnly fg.deobf("curse.maven:biomes-o-plenty-220318:3558882")
        runtimeOnly fg.deobf("curse.maven:rusticated-fruit-547435:3533340")
    }
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        codeChickenLib
    }
}

if (project.build_deobfJar.toBoolean()) {
    // Create deobf dev jars
    task deobfJar(type: Jar) {
        archiveClassifier.set("deobf")
        from sourceSets.main.output
    }
}

if (project.build_apiJar.toBoolean()) {
    // Create API library jar
    task apiZip(type: Jar) {
        archiveClassifier.set("api")
        from(sourceSets.main.java) {
            include "name/api/**"
        }
        from(sourceSets.main.output) {
            include "name/api/**"
        }
    }
}

if (project.build_sourceJar.toBoolean()) {
    // Create source jar
    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.allJava
    }
}

// Loading resources, works even on IDE client runs
sourceSets {
    main {
        if (project.use_mixins.toBoolean()) {
            ext.refMap = 'mixins.' + archives_base_name + '.refmap.json'
        }

        java {
            srcDirs = ["src/main/java"]
        }

        resources {
            srcDirs = ["src/main/java", "src/main/resources"]
        }
    }

    test {
        java {
            srcDirs = ["src/test/java"]
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
    }

    // at compile time, put resources in same directories as classes
    // main.output.setResourcesDir(main.java.outputDir)
}

mixin {
    add sourceSets.main, "mixins.mfrex.refmap.json"
    config 'mixins.mfrex.json'
}

jar {
    manifest {
        attributes([
                "Specification-Title": archivesBaseName,
                "Specification-Vendor": "PIPPIP5789",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": archivesBaseName,
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"PIPPIP5789",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePlugin": "com.PIPPIP5789.mfrex.mixin.MixinLoader",
                "FMLCorePluginContainsFMLMod": true
        ])
    }
}

artifacts {
    if (project.build_deobfJar.toBoolean()) {
        archives deobfJar
    }
    if (project.build_apiJar.toBoolean()) {
        archives apiZip
    }
    if (project.build_sourceJar.toBoolean()) {
        archives sourcesJar
    }
}

jar.finalizedBy('reobfJar')

processResources {
    // required to allow file expansion later
    duplicatesStrategy = 'include'

    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
